configurations {
    jaxb
}
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.3.RELEASE")
    }
}

group 'ru.progmatik'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

//bootJar {
//    baseName = 'fiasloader'
//    version =  '0.0.2'
//}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

// tag::wsdl[]
task genJaxb {
    ext.sourcesDir = "${buildDir}/generated-sources/jaxb"
    ext.classesDir = "${buildDir}/classes/jaxb"
    ext.schema = "https://fias.nalog.ru/WebServices/Public/DownloadService.asmx?wsdl"

    outputs.dir classesDir

    doLast() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
                    classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)
            mkdir(dir: classesDir)

            xjc(destdir: sourcesDir, schema: schema,
                    package: "fias.wsdl") {
                arg(value: "-wsdl")
                produces(dir: sourcesDir, includes: "**/*.java")
            }

            javac(destdir: classesDir, source: 1.8, target: 1.8, debug: true,
                    debugLevel: "lines,vars,source",
                    classpath: configurations.jaxb.asPath) {
                src(path: sourcesDir)
                include(name: "**/*.java")
                include(name: "*.java")
            }

            copy(todir: classesDir) {
                fileset(dir: sourcesDir, erroronmissingdir: false) {
                    exclude(name: "**/*.java")
                }
            }
        }
    }
}
// end::wsdl[]


dependencies {
    compileOnly "org.projectlombok:lombok:1.16.16"

    compile 'org.postgresql:postgresql:42.2.2'

    compile("org.springframework.boot:spring-boot-starter")

    // https://mvnrepository.com/artifact/com.github.junrar/junrar
    compile group: 'com.github.junrar', name: 'junrar', version: '2.0.0'

    compile ("org.firebirdsql.jdbc:jaybird-jdk18:3.0.4")

    compile("org.springframework.ws:spring-ws-core")

    compile(files(genJaxb.classesDir).builtBy(genJaxb))

// https://mvnrepository.com/artifact/org.apache.httpcomponents/httpclient
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.6'


    jaxb "com.sun.xml.bind:jaxb-xjc:2.1.7"

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

bootJar {
    baseName = 'fiasloader'
    version =  '0.1.3'

    from genJaxb.classesDir
}